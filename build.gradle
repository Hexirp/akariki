// ビルドスクリプトの設定をする。
buildscript {

  // ビルドスクリプトが使うリポジトリを指定する。
  repositories {

    jcenter ()

    maven { url = 'http://files.minecraftforge.net/maven' }

  }

  // ビルドスクリプトが依存するライブラリを指定する。
  dependencies {

    classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'

  }

}

// コアプラグインを使う。
plugins {

  id 'java'
  id 'scala'
  id 'idea'
  id 'eclipse'

}

// ForgeGradle プラグインを使う。
apply plugin: 'net.minecraftforge.gradle.forge'

// プロジェクトのプロパティを設定する。
version = '0.1.0-a-10'
group = 'io.github.hexirp'

// ソースコードとターゲットは Java 8 である。
sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

// wrapper には、その version がどうあれ all を使う。
wrapper {

  distributionType = Wrapper.DistributionType.ALL

}

// プロジェクトが使うリポジトリを指定する。
repositories {

  mavenCentral ()

}

// プロジェクトが依存するライブラリを指定する。
dependencies {

  // 注意: ForgeGradle により追加される依存関係は 'org.scala-lang:scala-library:2.11.1' を
  // 使用している。そのため、二つのライブラリが併存する形になる。これは targetCompatibility に
  // 1.8 を指定したときにコンパイルできない問題があり、それが 2.11.5 より修正されているため、
  // それ以上のバージョンを指定しなければいけなかったため。マイナーバージョンの違いであるため
  // 危険性が少ないと判断した。
  implementation 'org.scala-lang:scala-library:2.11.5'

}

// ForgeGradle の設定をする。
minecraft {

  // プロジェクトが依存する Minecraft Forge を指定する。
  version = '1.12.2-14.23.5.2768'
  mappings = 'snapshot_20171003'

  // プロジェクトを実際の Minecraft でデバッグするときのフォルダを指定する。
  runDir = 'run'

}

// コンパイル時のエンコーディングを UTF-8 にする
def encoding = { it.encoding = 'UTF-8' }
tasks.withType(AbstractCompile) each { encoding it.options }
